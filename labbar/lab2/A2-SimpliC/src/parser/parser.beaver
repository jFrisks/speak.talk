%class "LangParser";
%package "lang.ast";

%embed {:
	static public class SyntaxError extends RuntimeException { public SyntaxError(String msg) {super(msg);}}
	// Disable syntax error recovery
	protected void recoverFromError(Symbol token, TokenStream in) {
		throw new SyntaxError("Cannot recover from the syntax error");
	}
:};

%terminals ID, NUMERAL, INT, LPARAN, RPARAN, LBRACKET, RBRACKET, SEMICOLON, ASSIGN, MULTIPLIER, PLUS, SUB, DIVIDER, MODULAR;

%typeof program = "Program";
%typeof functionCallStmt = "FunctionCallStmt";
%typeof functionCallStmtList = "List";
%typeof stmt = "Stmt";
%typeof stmtList = "List";
%typeof block = "Block";
%typeof idDecl = "IdDecl";
%typeof assignment = "Assignment";
%typeof idUse = "IdUse";
%typeof numeral = "Numeral";
%typeof expr = "Expr";
%typeof factor = "Expr";
%typeof term = "Expr";
%typeof mul = "Mul";
%typeof add = "Add";
%typeof sub = "Sub";
%typeof div = "Div";
%typeof mod = "Mod";

%goal program;

program = functionCallStmtList.a {: return new Program(a); :} ;

functionCallStmtList =
  functionCallStmt.stmt {: return new List().add(stmt); :}
  | functionCallStmtList.list functionCallStmt.b {: return list.add(b); :}
  ;

functionCallStmt = INT ID LPARAN RPARAN block.b {: return new FunctionCallStmt(b); :} ;

block =
    LBRACKET stmtList.list RBRACKET {: return new Block(list); :}
    | LBRACKET RBRACKET {: return new Block(); :} ;

stmtList =
    stmt.a {: return new List().add(a); :}
    | stmtList.a stmt.b {: return a.add(b); :} ;

stmt =
  idDecl
  | assignment
  ;

assignment = idUse.a ASSIGN expr.b SEMICOLON {: return new Assignment(a,b); :};

expr =
  term
  | add
  | sub
  ;

add = expr.a PLUS term.b {: return new Add(a, b); :};

sub = expr.a SUB term.b {: return new Sub(a, b); :};

term =
    factor
    | div
    | mod
    | mul
    ;

mul = term.a MULTIPLIER factor.b {: return new Mul(a, b); :};

div = term.a DIVIDER factor.b {: return new Div(a, b); :};

mod = term.a MODULAR factor.b {: return new Mod(a, b); :};

factor =
  numeral
  | idUse;

idUse = ID.id {: return new IdUse(id); :};

numeral = NUMERAL.a {: return new Numeral(a); :};

idDecl = INT ID.id SEMICOLON {: return new IdDecl(id); :} ;
